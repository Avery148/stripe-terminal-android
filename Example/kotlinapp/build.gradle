apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

configurations {
    ktlint
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 28
        targetSdkVersion 29
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    buildToolsVersion = "29.0.0"
    compileSdkVersion = 29

    appcompatVersion = '1.1.0'
    constraintVersion = '1.1.3'
    constraintlayoutVersion = '1.1.3'
    coreTestingVersion = '2.1.0'
    coroutineVersion = '1.3.3'
    databindingVersion = '3.1.4'
    legacysupportVersion = '1.0.0'
    materialVersion = '1.0.0'
    okhttpVersion = '4.2.2'
    retrofitVersion = '2.7.0'
    stethoVersion = '1.5.1'
    viewModelVersion = '2.1.0'

    leakCanaryVersion = '2.0'

    // stripeTerminalVersion = 'set via publish.sh'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    api "com.google.android.material:material:$materialVersion"
    implementation "androidx.legacy:legacy-support-v4:$legacysupportVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$viewModelVersion"
    // We are using Kotlin, and not Java 8, so do not use the recommended Java 8 library
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:$viewModelVersion"
    testImplementation "androidx.arch.core:core-testing:$coreTestingVersion"

    // Databinding
    kapt "com.android.databinding:compiler:$databindingVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // OK HTTP
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    implementation "android.arch.persistence.room:runtime:1.1.1"
    implementation "com.google.dagger:dagger:2.26"
    implementation "com.google.guava:guava:28.1-android"
    implementation "com.jaredrummler:android-device-names:1.1.9"
    implementation "com.jakewharton.timber:timber:4.7.1"
    // Stripe Terminal library
//    implementation project(path: ":terminalsdk")
//    implementation project(path: ":terminalcotssdk")
    // Rootbeer
    implementation "com.scottyab:rootbeer-lib:0.0.7"

    // SafetyNet
    implementation "com.google.android.gms:play-services-safetynet:17.0.0"
    implementation "com.google.protobuf:protobuf-java:3.6.1"
    implementation "com.google.protobuf:protobuf-java-util:3.6.1"
    implementation "com.github.framgia:android-emulator-detector:1.4.0"

    // Leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

    // Ktlint
    ktlint "com.pinterest:ktlint:$ktlint_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
